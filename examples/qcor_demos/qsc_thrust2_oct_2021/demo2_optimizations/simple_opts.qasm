// Show off SSA-based quantum optimizations
//
// Show unoptimized
// qcor --emit-mlir simple_opts.qasm
// 
// Show optimized mlir
// qcor --emit-mlir --q-optimize simple_opts.qasm
//
// Show unoptimized LLVM/QIR
// qcor --emit-llvm -O0 simple_opts.qasm
//
// Show optimized LLVM/QIR
// qcor --emit-llvm -O3 simple_opts.qasm
//
// Show the passes run and timing
// (note the passes run, heuristic n_runs == 5 to pick up opts 
// generated by previous passes)
// qcor --emit-mlir --q-optimize simple_opts.qasm --pass-timing

OPENQASM 3;

// ID Pair Removal
qubit a[2];

x a[0];
x a[0];
cx a[0], a[1];
cx a[0], a[1];

// Rotation merging
qubit b[2];

// Merge X-Rx and Z-Rz
x b[0];
z b[1];
rx(1.2345) b[0];
rz(2.4566) b[1];

// Single qubit merge
qubit c;
h c;
z c;
h c;

// Inlining and Removal
gate oracle q_arg {
  x q_arg;
}

qubit d;
x d;
oracle d;

// Permute and Cancel
qubit e[2];

// Can permute, Rz's cancel, CX then cancels
rz(0.123) e[0];
cx e[0], e[1];
rz(-0.123) e[0];
cx e[0], e[1];

// Result should only have Rx(pi+1.2345), Rz(pi+2.4566), and Rx(pi)